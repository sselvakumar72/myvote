// groovy
plugins {
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'maven-publish'
}

ext {
    springBootVersion = '3.3.0'
    lombokVersion = '1.18.32'
    jjwtVersion = '0.11.5'
    logstashVersion = '7.4'
    commonsLangVersion = '3.18.0'
    commonsSdkVersion = '0.0.1-SNAPSHOT'
    javaVersion = 22
}

allprojects {
    group = 'com.lvt.apps'
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(javaVersion))
        }
        withSourcesJar()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web-services'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"
        implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
        implementation "com.lvt.apps:commons-sdk:${commonsSdkVersion}"

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.named('jar') {
        enabled = true
    }
}
